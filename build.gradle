plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
}

group 'uk.co.brightec'
version '2.0.0'
mainClassName = 'uk.co.brightec.xmlcheck.MainKt'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation "com.github.ajalt:clikt:2.1.0"

    testImplementation "org.junit.jupiter:junit-jupiter:5.5.1"
    testImplementation "io.mockk:mockk:1.9.3"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

task runMain(type: Exec, group: 'run') {
    executable "$buildDir/install/xmlcheck/bin/xmlcheck"
    args = [
            "./src/test/resources/files/cases"
    ]
}

task runMainHelp(type: Exec, group: 'run') {
    executable "$buildDir/install/xmlcheck/bin/xmlcheck"
    args = [
            "--help"
    ]
}

task installAndRun(group: 'run') {
    dependsOn installDist
    dependsOn runMain
    runMain.mustRunAfter installDist
}

task installAndHelp(group: 'run') {
    dependsOn installDist
    dependsOn runMainHelp
    runMainHelp.mustRunAfter installDist
}

task distJar(type: Jar, group: 'distribution') {
    destinationDirectory = new File('build/distributions')

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}